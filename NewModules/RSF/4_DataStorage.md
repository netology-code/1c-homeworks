# Задание к занятию «Хранилище значения, потоки»

*Примерное время выполнения: 20–60 минут*

## Цель задания

1. ????
2. Подготовить конфигурацию к последующей работе.

## Чеклист готовности к домашнему заданию

- [ ] Установить учебную платформу версии 8.3.22 или больше.
- [ ] Подготовить разработанную ранее конфигурацию "УправлениеИТФирмой"
- [ ] Просмотреть материал занятия «Хранилище значения, потоки».

## Инструкция к заданию

1. Решите описанные задачи в конфигураторе.

В тексте задания приведен программный код для решения задач. Конечно, Вы можете использовать его. Но для более продуктивного обучения, старайтесь, сначала, самостоятельно написать код. Если все же, самостоятельно решить задачу не удалось, используйте код из текста задачи но обязательно пройдите его в отладке, посмотрите, что попадает в переменные, как преобразуются значения. Старайтесь максимально детально разобраться в механизме.

2. Протестируйте решение в пользовательском режиме, обязательно введите данные в базу, убедитесь, что все работает.
3. Отправьте на проверку в личном кабинете Нетологии один общий файл базы данных (.dt), содержащей решение всех задач.

## Задача 1. Фотография сотрудника

### Описание задачи

Реализуем возможность добавлять к справочнику Сотрудники фотографию

### Процесс выполнения

1. Добавьте в справочник реквизит "Фотография" с типом ХранилищеЗначения.
2. На форме справочника, создайте реквизит АдресФото с типом Строка.
3. Выведите реквизит на форму, вид реквизита установите "Поле картинки", разместите элементы на форме, чтобы они смотрелись максмально органично
4. В элементе формы укажите:

- Установите флаг "Гиперссылка" - так, мы сможем обработать событие нажатия на поле картинки.
- Задайте ширину и высоту поля при необходимости.
- Установите в РазмерКартинки значение "Пропорционально", чтобы картинка меняла свой размер в зависимости от размера поля.
- ТекстНевыбраннойКартинки установите "Нажмите для выбора фото"

5. Создайте обработчик для события Нажатие поля картинки. В нем откройте диалог выбора файлов с фильтром по типам картинок, полученный файл поместите во временное хранилище.
Все вышеописанные действия можно сделать при помощи метода "ПоместитьФайлНаСерверАсинх"

<details>
  <summary>Код</summary>

```bsl
&НаКлиенте
Процедура АдресФотоНажатие(Элемент, СтандартнаяОбработка)

    // отключаем стандартную обработку, т.к. стандартное действие для обработки нажатия на строку - вывести ее содержимое в предупреждении, нам это не нужно.
	СтандартнаяОбработка = Ложь;
	ВыбратьФайлФото();

КонецПроцедуры

&НаКлиенте
Асинх Процедура ВыбратьФайлФото()

	ПараметрыДиалога = Новый ПараметрыДиалогаПомещенияФайлов;
	ПараметрыДиалога.Заголовок = "Выберите фотографию";
	ПараметрыДиалога.Фильтр = "Картинки|*.jpg; *.png";
	ПараметрыДиалога.МножественныйВыбор = Ложь;

	ОписаниеФайла = Ждать ПоместитьФайлНаСерверАсинх( , , , ПараметрыДиалога, УникальныйИдентификатор);

	Если ОписаниеФайла = Неопределено Тогда
		Возврат;
	КонецЕсли;

	АдресФото = ОписаниеФайла.Адрес;
КонецПроцедуры
```

</details>

6. Создайте обработчик формы ПередЗаписьюНаСервере, в котором поместите картинку в хранилище значения и сохраните в реквизит Фотография

<details>
  <summary>Код</summary>

```bsl
&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)

	ДвоичныеДанныеФотографии = ПолучитьИзВременногоХранилища(АдресФото);
	ДанныеВХранилище = Новый ХранилищеЗначения(ДвоичныеДанныеФотографии);
	ТекущийОбъект.Фотография = ДанныеВХранилище;

КонецПроцедуры
```

</details>

7. При чтении на сервере, необходимо попытаться получить картинку, поместить ее во временное хранилище и заполнить реквизит формы

<details>
  <summary>Код</summary>

```bsl
&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	ДвоичныеДанныеФотографии = ТекущийОбъект.Фотография.Получить();
	АдресФото = ПоместитьВоВременноеХранилище(ДвоичныеДанныеФотографии, УникальныйИдентификатор);

КонецПроцедуры
```

</details>

8. Убедитесь, что картинка в пользовательском режиме устанавливается, сохраняется в базе данных и при повторном открытии формы, заполняется на форму.

## Задача 2. Обработка двоичных данных

### Описание задачи

Добавить возможность сохранения в файл фото сотрудника с шириной 100 пикселей.

### Процесс выполнения

1. Добавьте на форму элемента команду "Сохранить", органично разместите ее на форме.
2. При нажатии на кнопку, получите файл картинки, проверьте ее текущую ширину и если она превышает 100 пикселей, уменьшите ширину через объект ОбрабатываемаяКартинка и запишите картинку в файл. Файл сохраните по указанному пользователем пути.

<details>
  <summary>Код</summary>

```bsl
&НаКлиенте
Процедура СохранитьФото(Команда)

	АдресРезультата = ПодготовитьКартинку(АдресФото, УникальныйИдентификатор);

	// Если для номенклатуры не загружена картика, то не будем открывать окно сохранения файла
	Если Не ЗначениеЗаполнено(АдресРезультата) Тогда
		Возврат;
	КонецЕсли;

	ПараметрыПолученияФайла = Новый ПараметрыДиалогаПолученияФайлов;
	ПолучитьФайлССервераАсинх(АдресРезультата, Объект.Наименование + ".jpg", ПараметрыПолученияФайла);

КонецПроцедуры

&НаСервереБезКонтекста
Функция ПодготовитьКартинку(АдресФото, УИДФормы)

	ДвоичныеДанныеИзображения = ПолучитьИзВременногоХранилища(АдресФото);

	Если ДвоичныеДанныеИзображения = Неопределено Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Фотография не загружена";
		Сообщение.Сообщить();
		Возврат Неопределено;
	КонецЕсли;

	Картинка = Новый Картинка(ДвоичныеДанныеИзображения);                              

	Если Картинка.Ширина() = 100 Тогда 
		// Если ширина уже 100 пикселей, не надо обрабатывать, просто вернем тот же адрес
		Возврат АдресФото;
	КонецЕсли;

	ОбрабатываемаяКартинка = Новый ОбрабатываемаяКартинка(Картинка);
	ОбрабатываемаяКартинка.УстановитьРазмер(100, Неопределено);

	Картинка = ОбрабатываемаяКартинка.ПолучитьКартинку();

	ДвоичныеДанныеИзображения = Картинка.ПолучитьДвоичныеДанные();

	АдресОбработаннойКартинки = ПоместитьВоВременноеХранилище(ДвоичныеДанныеИзображения, УИДФормы);
	Возврат АдресОбработаннойКартинки;

КонецФункции
```

</details>

3. Убедитесь, что сохраненная картинка имеет ширину 100 пикселей.

## Задача 3*. Удаление фотографии

*Это дополнительная задача, реализовывать ее не обязательно.*
*Задача предназначена для тех студентов, которым первые покажутся слишкм простыми.*
*В процессе выполнения не будут даны примеры программного кода.*

### Описание задачи

Добавьте возможность удаления фото сотрудника

### Процесс выполнения

1. Добавьте кнопку для удаления фотографии
2. При нажатии на кнопку, надо очистить фотографию на форме
3. Обратите внимание, что если пользователь удалил фотографию, а потом закрыл форму не записывая элемент справочника, фотография из базы удалиться не должна.

## Задача 4*. Потоки

*Это дополнительная задача, реализовывать ее не обязательно.*
*Задача предназначена для тех студентов, которым первые покажутся слишкм простыми.*
*В процессе выполнения не будут даны примеры программного кода.*

### Описание задачи

Для тренировки работы с потоками, создайте обработку, которая разбивала бы указанный для нее файл на отдельные части и собирала из частей исходный файл.

На самом деле, подобный функционал уже есть в платформе - изучите системные глобальные методы «РазделитьФайл» и «ОбъединитьФайлы». Но цель задачи - сделать это самостоятельно, не используя эти функции.

### Процесс выполнения

1. Создайте обработку "Работа с потоками" разместите ее в подсистеме "Отчетность"
2. На форме обработки сделайте 2 закладки "Разбиение файла на части" и "Сборка файла"
3. Добавьте строковый реквизит "ПутьКИсходномуФайлу". Выведите его на страницу "Разбиение файла на части" пропишите обработку для выбора файла
4. Добавьте строковый реквизит "КаталогСЧастямиФайла". Выведите его на страницу "Разбиение файла на части" пропишите обработку для выбора каталога
5. Добавьте числовой реквизит "РазмерЧастиВБайтах". Выведите его на страницу "Разбиение файла на части"
6. Добавьте команду "РазбитьФайлНаЧасти". Выведите команду на страницу "Разбиение файла на части", пропишите обработчик нажатия в котором:

- Откройте фаловый поток файла-источника
- Пока возможно прочитать файл считывайте очередную порцию байтов (размер указан в реквизите РазмерЧастиВБайтах) создавайте файловый поток приемник в каталоге с частями, копируйте данные из потока источника в поток приемник, закрывайте поток приемник.

<details>
  <summary>Подсказка по алгоритму разбиения</summary>

- Выяснить размер исходного файла и записать в переменную «ОсталосьПрочитать». См. объект «Файл» и «ФайловыйПоток».
- В цикле открыть новый файловый поток для записи. Имя части сгенерировать как "Часть_"+"Счетчик.
- Записать в файловый поток части методом ИсходныйПоток.КопироватьВ(ПотокЧасти, РазмерЧасти).
- Перед записью проконтролировать, что РазмерЧасти меньше переменной ОсталосьПрочитать. Если РазмерЧасти больше — скопировать только оставшееся число байт
- Уменьшить переменную ОсталосьПрочитать на РазмерЧастиВБайтах.

</details>

6. Создайте на форме таблицу "ЧастиФайла". Таблица будет содержать только 1 колонку "ПутьКЧасти" с типом Строка
7. Выведите таблицу на страницу "Сборка файла". Для элемента ПутьКЧасти добавьте возможность указывать файл через диалог выбора файлов.
8. Добавьте строковый реквизит "ПутьКРезультирующемуФайлу". Выведите его на страницу "Сборка файла" пропишите обработку для выбора файла. (в данном случа, следует использовать режим диалога "Сохранение")
9. Добавьте команду "СобратьФайл". Выведите команду на страницу "Сборка файла", пропишите обработчик нажатия в котором:

- Открыть для записи поток в файл из поля ПутьКРезультирующемуФайлу.
- В цикле по таблице ЧастиФайла открывать потоки для чтения текущей строки таблицы и копировать их в поток-результат методом ПотокИсточник.КопироватьВ.
- Количество байт контролировать в этом случае не надо.

## Пример

[Пример выполнения домашнего задания](examples/HW_6_4_example.md)

## Критерии оценки

Зачёт ставится, если:

1. Программа запускается, не возникает явных ошибок, исключений при выполнении программы (в том числе, если Вы начали делать дополнительную задачу, ее функционал не должен приводить к ошибкам и исключениям)
2. К справочнику Сотрудники можно загрузить фотографию
3. Фотография сохраняется в базе данных
4. Ранее выбранную картинку можно сохранить в файл, при этом ширина сохранённой картинки будет 100 пикселей.
5. Введены тестовые данные

Задачи 1 и 2 обязательны к выполнению (кроме текста под спойлером "Дополнительно" - эти задачи делать не обязательно. Возможно, Вы вернетесь к ним позднее, после того, как изучите дополнительный материал). Задачи 3 и 4 не обязательны

Пожалуйста, присылайте на проверку все задачи сразу, одним файлом выгрузки информационной базы (dt)

Любые вопросы по решению задач задавайте в чате учебной группы.
