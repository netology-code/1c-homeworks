# Задание к занятию «Работа с файлами на сервере»

Все задачи обязательны к выполнению (кроме текста под спойлером "Дополнительно" - эти задачи делать необязательно. Возможно, Вы вернетесь к ним позднее, после того, как изучите дополнительный материал).

Пожалуйста, присылайте на проверку все задачи сразу, одним файлом выгрузки информационной базы (dt)
Любые вопросы по решению задач задавайте в чате учебной группы.

## Цель задания

1. Изучить подходы к передачи файлов с клиента на сервер.
2. На практике изучить прикрепление файлов к базе данных.
3. Подготовить конфигурацию к последующей работе.

## Чеклист готовности к домашнему заданию

- [ ] Установить учебную платформу версии 8.3.22 или больше.
- [ ] Подготовить разработанную ранее конфигурацию "УправлениеИТФирмой"
- [ ] Просмотреть материал занятия «Работа с файлами на сервере, Двоичные данные».

## Инструкция к заданию

1. Решите описанные задачи в конфигураторе.

В тексте задания приведен программный код для решения задач. Конечно, Вы можете использовать его. Но для более продуктивного обучения, старайтесь, сначала, самостоятельно написать код. Если все же, самостоятельно решить задачу не удалось, используйте код из текста задачи но обязательно пройдите его в отладке, посмотрите, что попадает в переменные, как преобразуются значения. Старайтесь максимально детально разобраться в механизме.

2. Протестируйте решение в пользовательском режиме, обязательно введите данные в базу, убедитесь, что все работает.
3. Отправьте на проверку в личном кабинете Нетологии один общий файл базы данных (.dt), содержащей решение всех задач, а так же, файл csv, на данных которого Вы тестируете загрузку.

## Задача 1. Обработка файла на сервере

### Описание задачи

Доработаем обработку загрузки прайслиста таким образом, чтобы обработка выполнялась на стороне сервера. Для этого передадим файл с клиента на сервер, прочитаем его там, пытаясь сразу найти ссылку на номенклатуру, если не вся номенклатура найдена, зададим пользователю вопрос, о необходимости создания документа, но при этом данные файла с сервера на клиент передавать не будем.
Визуально, для пользователя работа обработки измениться не должна.

### Процесс выполнения

1. Перепишем процедуру загрузки файла

- Поместим файл во временное хранилище
- вызовем метод загрузки, передав в него только адрес файла во временном хранилище

2. На сервере
  
- получим данные файла из временного хранилища
- запишем во временный файл
- считаем данные файла, сразу пытаясь найти номенклатуру
- поместим данные файла во временное хранилище
- при необходимости, зададим пользователю вопрос на клиенте
- загрузим данные в документ

<details>
  <summary>Код</summary>

```bsl
&НаКлиенте
Асинх Процедура ЗагрузитьФайл()

	ОписаниеФайла = Ждать ПоместитьФайлНаСерверАсинх(, , , Объект.ПутьКФайлу, УникальныйИдентификатор);
	РезультатЗагрузки = ЗагрузитьФайлНаСервере(ОписаниеФайла.Адрес, УникальныйИдентификатор);

	Если Не РезультатЗагрузки.ВсяНоменклатураНайдена Тогда

		ТекстВопроса = "Не все номенклатурные позиции из файла существуют в справочнике. Продолжить создание документа?";
		ОтветПользователя = Ждать ВопросАсинх(ТекстВопроса, РежимДиалогаВопрос.ДаНет);

		Если ОтветПользователя = КодВозвратаДиалога.Нет Тогда
			Возврат;
		КонецЕсли;

	КонецЕсли;  

	СоздатьДокумент(Объект.Контрагент, РезультатЗагрузки.АдресДанныхФайла);	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗагрузитьФайлНаСервере(АдресФайлаВоВременномХранилище, УникальныйИдентификатор)

	ДвоичныеДанныеФайла = ПолучитьИзВременногоХранилища(АдресФайлаВоВременномХранилище);
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла("csv");
	ДвоичныеДанныеФайла.Записать(ИмяВременногоФайла);

	РезультатЗагрузки = Новый Структура("ВсяНоменклатураНайдена, АдресДанныхФайла", Истина, Неопределено);
	ПрочитатьФайл(ИмяВременногоФайла, РезультатЗагрузки, УникальныйИдентификатор);

	// Очистим ненужные объекты
	УдалитьФайлы(ИмяВременногоФайла);
	УдалитьИзВременногоХранилища(АдресФайлаВоВременномХранилище);

	Возврат РезультатЗагрузки;
КонецФункции

&НаСервереБезКонтекста
Процедура ПрочитатьФайл(ИмяВременногоФайла, РезультатЗагрузки, УникальныйИдентификатор)

	ТекстовыйФайлЗагрузки = Новый ТекстовыйДокумент;
	ТекстовыйФайлЗагрузки.Прочитать(ИмяВременногоФайла, КодировкаТекста.UTF8);

	ДанныеФайла = Новый Массив;
	ШаблонСообщения = НСтр("ru = 'Номенклатура: %1 не найдена'");

	//Прочитаем строки файла
	Для НомерСтроки = 1 по ТекстовыйФайлЗагрузки.КоличествоСтрок() Цикл

		НоваяСтрока = ТекстовыйФайлЗагрузки.ПолучитьСтроку(НомерСтроки);

		// «парсим» строки по ";"
		// ищем позицию символа-разделителя
		Позиция = Найти(НоваяСтрока, ";");

		// Получаем из строки наименование номенклатуры и цену
		// Наименование перед символом-разделителем, цена - после
		НаименованиеНоменклатуры = Сред(НоваяСтрока, 1, Позиция - 1);
		Цена = Сред(НоваяСтрока, Позиция + 1);

		// Готовим коллекцию данных для последующего заполнения документа
		ДанныеСтрокиДокумента = Новый Структура;
		ДанныеСтрокиДокумента.Вставить("НаименованиеНоменклатуры", НаименованиеНоменклатуры);
		ДанныеСтрокиДокумента.Вставить("Цена", Цена);

		// Сразу, ищем номенклатуру 
		Номенклатура = Справочники.Номенклатура.НайтиПоНаименованию(НаименованиеНоменклатуры); 
		Если Не ЗначениеЗаполнено(Номенклатура) Тогда

			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = СтрШаблон(ШаблонСообщения, НаименованиеНоменклатуры);
			Сообщение.Сообщить();

			РезультатЗагрузки.ВсяНоменклатураНайдена = Ложь;
		КонецЕсли;
		ДанныеСтрокиДокумента.Вставить("НоменклатураСсылка", Номенклатура);
		
		ДанныеФайла.Добавить(ДанныеСтрокиДокумента);

	КонецЦикла;

	РезультатЗагрузки.АдресДанныхФайла = ПоместитьВоВременноеХранилище(ДанныеФайла, УникальныйИдентификатор);

КонецПроцедуры

&НаСервереБезКонтекста
Процедура СоздатьДокумент(Контрагент, АдресДанныхФайла)

	// Создаём новый документ
	ДокументЦены = Документы.УстановкаЦен.СоздатьДокумент();
	ДокументЦены.Дата = ТекущаяДата();
	ДокументЦены.Контрагент = Контрагент;
	ДокументЦены.Комментарий = "Загружен из файла";

	ДанныеФайла = ПолучитьИзВременногоХранилища(АдресДанныхФайла);
	// Обходим коллекцию с данными файла и заполняем строки табличной части
	Для Каждого ДанныеСтроки Из ДанныеФайла Цикл

		// Второй раз искать номенклатуру не надо, берем из структуры
		Номенклатура = ДанныеСтроки.НоменклатураСсылка;

		Если Не ЗначениеЗаполнено(Номенклатура) Тогда
			Продолжить;
		КонецЕсли;

		НоваяСтрокаТЧ = ДокументЦены.Цены.Добавить();
		НоваяСтрокаТЧ.Номенклатура = Номенклатура;
		НоваяСтрокаТЧ.Цена = ДанныеСтроки.Цена;

	КонецЦикла;

	ДокументЦены.Записать(РежимЗаписиДокумента.Запись);

	УдалитьИзВременногоХранилища(АдресДанныхФайла);
КонецПроцедуры
```

</details>

## Задача 2. Преобразование файла в Base64 и обратно

### Описание задачи

В учебных целях, реализуем обработку, которая будет создавать строку из файла, и наоборот, из строки создавать файл.

### Процесс выполнения

1. Создайте обработку "Работа с Base64" разместите ее в подсистеме Отчетность
2. На форме обработки создайте 2 реквизита с типом Строка неограниченной длины - "Путь к файлу" и "Строка Base64"
3. Выведите реквизиты на форму один под другим.
4. Для "Путь к файлу" отобразите кнопки выбора и очистки. Опишите обработчик для выбора файла, через Диалог выбора файла

<details>
  <summary>Код</summary>

```bsl
&НаКлиенте
Процедура ПутьКФайлуНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ВыбратьФайлАсинхронно();
КонецПроцедуры

&НаКлиенте
Асинх Процедура ВыбратьФайлАсинхронно()

	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	Диалог.Заголовок = "Выберите файл";
	
	РезультатВыбора = Ждать Диалог.ВыбратьАсинх();
	
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПутьКФайлу = РезультатВыбора[0];
КонецПроцедуры
```

</details>

5. Для "Строка Base64" установите многострочный режим отображения и отключите Автоматическое определение максимальной ширины
6. Создайте команды "Прочитать в Base64" и "Записать из Base64", разместите кнопки на форме
7. Опишите обработчик для "Прочитать в Base64" - получите двоичные данные файла и преобразуйте их в строку, строку запишите в реквизит

<details>
  <summary>Код</summary>

```bsl
&НаКлиенте
Процедура ПрочитатьВBase64(Команда)

	ДанныеФайла = Новый ДвоичныеДанные(ПутьКФайлу);
	СтрокаBase64 = Base64Строка(ДанныеФайла);
КонецПроцедуры
```

</details>

8. Опишите обработчик для "Записать из Base64" - получите двоичные данные файла из строки, запишите двоичные данные по пути, указанному пользователем.

<details>
  <summary>Код</summary>

```bsl
&НаКлиенте
Процедура ЗаписатьИзBase64(Команда)

	ЗначениеИзСтроки = Base64Значение(СтрокаBase64);
	ЗначениеИзСтроки.ЗаписатьАсинх(ПутьКФайлу);
КонецПроцедуры
```

</details>

## Пример

[Пример выполнения домашнего задания](examples/HW_6_2_example.md)

## Критерии оценки

Зачёт ставится, если:

1. Программа запускается, не возникает явных ошибок, исключений при выполнении программы (в том числе, если вы начали делать дополнительную задачу, её функционал не должен приводить к ошибкам и исключениям)
2. При загрузке цен, файл передаётся на сторону сервера, обратно на клиент передается только строка с адресом во временном хранилище, данные файла не передаются
3. Данные корректно преобразуются в строку Base64, корректно восстанавливаются обратно
4. Введены тестовые данные

