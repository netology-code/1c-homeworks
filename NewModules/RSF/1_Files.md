# Задание к занятию «Файлы»

Все задачи обязательны к выполнению (кроме текста под спойлером "Дополнительно" - эти задачи делать необязательно. Возможно, Вы вернетесь к ним позднее, после того, как изучите дополнительный материал).

Пожалуйста, присылайте на проверку все задачи сразу, одним файлом выгрузки информационной базы (dt)


## Цель задания

1. Изучить работу с файлами.
2. Научиться прочитать данные из файла и записать данные из базы в файл.
3. Подготовить конфигурацию к последующей работе.

## Чеклист готовности к домашнему заданию

- [ ] Установить учебную платформу версии 8.3.22 или больше.
- [ ] Подготовить разработанную ранее конфигурацию "УправлениеИТФирмой"
- [ ] Просмотреть материал занятия «Файлы».

## Инструкция к заданию

1. Решите описанные задачи в конфигураторе.

В тексте задания приведен программный код для решения задач. Конечно, Вы можете использовать его. Но для более продуктивного обучения, старайтесь, сначала, самостоятельно написать код. Если все же, самостоятельно решить задачу не удалось, используйте код из текста задачи но обязательно пройдите его в отладке, посмотрите, что попадает в переменные, как преобразуются значения. Старайтесь максимально детально разобраться в механизме.

2. Протестируйте решение в пользовательском режиме, обязательно введите данные в базу, убедитесь, что все работает.
3. Отправьте на проверку в личном кабинете Нетологии один общий файл базы данных (.dt), содержащей решение всех задач, а так же, файл csv, на данных которого Вы тестируете загрузку.

## Задача 1. Создать обработку для загрузки цен из файла

### Описание задачи

Поставщики присылают цены на свою продукцию в виде csv-файлов. Нам необходимо загружать эти прайслисты в систему.
Нам необходимо создать обработку, в которой пользователь сможет выбрать файл в формате csv и поставщика из справочника, нажать на кнопку "Загрузить", после чего в системе должен быть создан новый документ УстановкаЦен, содержащий в табличной части информацию из файла

### Подготовка

csv - это простейший способ описать таблицу в файле. Подробное описание формата можно прочитать [здесь](https://ru.wikipedia.org/wiki/CSV)
Файл содержит строки соответствующие строкам таблицы, колонки в строках разделяются при помощи специального символа, как правило, точки с запятой ";"
Создайте файл csv, который будете использовать для загрузки.

1. В конфигураторе нажмите Файл -> Новый -> Выберите "Текстовый документ"
2. Введите данные. Каждая строка файла - это строка таблицы, колонки отделяются друг от друга символом ";". В первой колонке пропишите название номенклатуры (оно должно досимволно совпадать с наименованием в справочнике), во второй колонке - цену, которая будет установлена. Внесите в файл не менее двух существующих в справочнике номенклатур и как минимум одну, не существующую.

<details>
  <summary>Пример</summary>

```csv
Видеокарта;50000
Оперативная память;10000
Куллер;7500
```

</details>

<details>
  <summary>Пример файла</summary>

Можно скачать пример файла [здесь](src/Pricelist.csv)

</details>

1. Сохраните файл (Файл -> Сохранить). При сохранении укажите формат csv (В поле Имя файла укажите любое имя файла, но в конце пропишите ".csv", например "ПрайсЛистПоставщиова.csv")
2. Не забудьте вместе с решением прислать этот файл для тестирования вашего решения.

### Процесс выполнения

1. Создайте обработку "ЗагрузкаПрайслистаКонтрагента", разместите ее в подсистеме Сделки, настройте права. Создайте реквизиты "Контрагент" (с соответствующим типом данных) и "ПутьКФайлу" (Строка не ограниченной длины) Реквизиты должны быть обязательными к заполнению
2. Создайте форму обработки, вынесите на форму реквизиты.
3. Для поля ввода пути к файлу, отобразите кнопки Выбора и Очистки. Задайте обработчик события НачалоВыбора, в котором:

- Откройте диалог выбора файлов с фильтром по csv-файлам
- После закрытия диалога, если пользователь выбрал файл, заполните путь к нему в соответствующий реквизит

<details>
  <summary>Код</summary>

```bsl
&НаКлиенте
Процедура ПутьКФайлуНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ВыбратьФайлАсинхронно();
КонецПроцедуры

&НаКлиенте
Асинх Процедура ВыбратьФайлАсинхронно()

	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	Диалог.Заголовок = "Выберите файл для загрузки данных";
	Диалог.Фильтр = "CSV-файлы|*.csv";
	
	РезультатВыбора = Ждать Диалог.ВыбратьАсинх();
	
	Если РезультатВыбора = Неопределено Тогда
		// Пользователь ничего не выбрал
		Возврат;
	КонецЕсли;
	
	Объект.ПутьКФайлу = РезультатВыбора[0];

КонецПроцедуры
```

</details>

4. Добавьте команду "Загрузить", вынесите ее на форму кнопкой по умолчанию. В обработчике нажатия на кнопку:

- Проверьте, что реквизиты заполнены. Если нет - прервите выполнение кода.
- При помощи текстового документа, прочитайте файл:
  - создайте массив для помещения в него результатов чтения файла
  - создайте новый текстовый документ
  - командой ПрочитатьАсинх прочитайте данные файла
  - считывайте строки файла последовательно создавайте структуру с ключами НаименованиеНоменклатуры и Цена, заполняйте в них значения из строки.
    - значения получайте при помощи функций работы со строками Найти, Сред
- Полученый на предыдущем шаге массив структур, а так же контрагента из реквизита передайте на сервер
- Создайте новый документ, заполните у него дату и контрагента
- В цикле перебирайте массив, для каждой номенклатуры ищите ссылку в справочнике.
  - если ссылка не найдена, пропускайте элемент массива, переходите к следующему
  - если ссылка найдена - создайте новую строку табличной части, заполните значения

<details>
  <summary>Код обработчика команды</summary>

```bsl
&НаКлиенте
Процедура Загрузить(Команда)

	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	ЗагрузитьФайл();

КонецПроцедуры

&НаКлиенте
Асинх Процедура ЗагрузитьФайл()

	ДанныеФайла = Ждать ПрочитатьФайл(Объект.ПутьКФайлу);
	СоздатьДокумент(Объект.Контрагент, ДанныеФайла);

КонецПроцедуры
```

</details>

<details>
  <summary>Код чтения файла</summary>

```bsl
&НаКлиенте
Асинх Функция ПрочитатьФайл(ПутьКФайлу)

	ТекстовыйФайлЗагрузки = Новый ТекстовыйДокумент;
	Ждать ТекстовыйФайлЗагрузки.ПрочитатьАсинх(ПутьКФайлу, КодировкаТекста.UTF8);
	Результат = Новый Массив;

	//Прочитаем строки файла
	Для НомерСтроки = 1 по ТекстовыйФайлЗагрузки.КоличествоСтрок() Цикл

		НоваяСтрока = ТекстовыйФайлЗагрузки.ПолучитьСтроку(НомерСтроки);

		// «парсим» строки по ";"
		// ищем позицию символа-разделителя
		Позиция = Найти(НоваяСтрока, ";");

		// Получаем из строки наименование номенклатуры и цену
		// Наименование перед символом-разделителем, цена - после
		НаименованиеНоменклатуры = Сред(НоваяСтрока, 1, Позиция - 1);
		Цена = Сред(НоваяСтрока, Позиция + 1);

		// Готовим коллекцию данных для последующего заполнения документа
		ДанныеСтрокиДокумента = Новый Структура;
		ДанныеСтрокиДокумента.Вставить("НаименованиеНоменклатуры", НаименованиеНоменклатуры);
		ДанныеСтрокиДокумента.Вставить("Цена", Цена);

		Результат.Добавить(ДанныеСтрокиДокумента);

	КонецЦикла;	

	Возврат Результат;

КонецФункции
```

</details>

<details>
  <summary>Код создания документа</summary>

```bsl
&НаСервереБезКонтекста
Процедура СоздатьДокумент(Контрагент, ДанныеФайла)

	// Создаём новый документ
	ДокументЦены = Документы.УстановкаЦен.СоздатьДокумент();
	ДокументЦены.Дата = ТекущаяДата();
	ДокументЦены.Контрагент = Контрагент;
	ДокументЦены.Комментарий = "Загружен из файла";

	ШаблонСообщения = НСтр("ru = 'Номенклатура: %1 не найдена'");

	// Обходим коллекцию с данными файла и заполняем строки табличной части
	Для Каждого ДанныеСтроки Из ДанныеФайла Цикл

		Номенклатура = Справочники.Номенклатура.НайтиПоНаименованию(ДанныеСтроки.НаименованиеНоменклатуры);

		// Если номенклатура не найдена, то сообщаем об этом пользователю
		Если Не ЗначениеЗаполнено(Номенклатура) Тогда

			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = СтрШаблон(ШаблонСообщения, ДанныеСтроки.НаименованиеНоменклатуры);
			Сообщение.Сообщить();

			Продолжить;

		КонецЕсли;

		НоваяСтрокаТЧ = ДокументЦены.Цены.Добавить();
		НоваяСтрокаТЧ.Номенклатура = Номенклатура;
		НоваяСтрокаТЧ.Цена = ДанныеСтроки.Цена;

	КонецЦикла;	

	ДокументЦены.Записать();

КонецПроцедуры
```

</details>

5. Убедитесь, что документы создаются, если номенклатура не найдена, ее цена в документ попасть не должна.

## Задача 2. Развитие обработки для загрузки цен из файла

### Описание задачи

Доработаем обработку. Если при загрузке файла выясняется, что хотя бы одной номенклатуры нет в базе, задайте вопрос пользователю с текстом «Не все номенклатурные позиции из файла существуют в справочнике. Продолжить создание документа?» и вариантами ответа «Да» и «Нет».
- При ответе «Да» — записывать документ.
- При ответе «Нет» — приостанавливать работу алгоритма.

### Процесс выполнения

Начнем с построения схемы решения. Очевидно, искать номенклатуру мы можем только на стороне сервера (т.к. только на сервере доступна информационная база), а взаимодействовать с пользователем - только на стороне клиента.

Значит, нам необходимо резделить загрузку на 2 отдельных процесса - поиск номенклатуры и непосредственно создание документа.

Доработаем код обработки следующим образом:

1. После чтения данных файла, передадим их на сервер, попытаемся найти ссылки на номенклатуру. В каждую структуру, которая является элементом массива будем добавлять дополнительное поле НоменклатураСсылка, заполняя его.
2. Если все номенклатурные позиции найдены, вернем Истина, в противном случае Ложь.
3. Если не все позиции найдены - зададим пользователю вопрос о необходимости продолжить обработку.
4. Если все позиции найдены, вопрос задаваться не должен.
5. В процедуру создания документа, передадим массив. Теперь, при создании документа не надо снова искать номенклатуру. Ее можно просто брать из соответствующего поля структуры.

<details>
  <summary>Код обработчика команды</summary>

```bsl
&НаКлиенте
Процедура Загрузить(Команда)

	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	ЗагрузитьФайл();

КонецПроцедуры

&НаКлиенте
Асинх Процедура ЗагрузитьФайл()

	ДанныеФайла = Ждать ПрочитатьФайл(Объект.ПутьКФайлу);
	ВсяНоменклатураНайдена = НайтиНоменклатуру(ДанныеФайла);
	Если Не ВсяНоменклатураНайдена Тогда
		ТекстВопроса = "Не все номенклатурные позиции из файла существуют в справочнике. Продолжить создание документа?";
		ОтветПользователя = Ждать ВопросАсинх(ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		Если ОтветПользователя = КодВозвратаДиалога.Нет Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	СоздатьДокумент(Объект.Контрагент, ДанныеФайла);

КонецПроцедуры
```

</details>

<details>
  <summary>Код процедуры поиска номенклатуры</summary>

```bsl
&НаСервереБезКонтекста
Функция НайтиНоменклатуру(ДанныеФайла)
	
	ВсяНоменклатураНайдена = Истина;
	ШаблонСообщения = НСтр("ru = 'Номенклатура: %1 не найдена'");

	Для Каждого ДанныеСтроки Из ДанныеФайла Цикл

		Номенклатура = Справочники.Номенклатура.НайтиПоНаименованию(ДанныеСтроки.НаименованиеНоменклатуры);
		Если Не ЗначениеЗаполнено(Номенклатура) Тогда

			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = СтрШаблон(ШаблонСообщения, ДанныеСтроки.НаименованиеНоменклатуры);
			Сообщение.Сообщить();

			ВсяНоменклатураНайдена = Ложь;
		КонецЕсли;

		// запомним в структуре найденную номенклатуру
		ДанныеСтроки.Вставить("НоменклатураСсылка", Номенклатура);

	КонецЦикла;

	Возврат ВсяНоменклатураНайдена;

КонецФункции
```

</details>

<details>
  <summary>Код создания документа</summary>

```bsl
&НаСервереБезКонтекста
Процедура СоздатьДокумент(Контрагент, ДанныеФайла)

	// Создаём новый документ
	ДокументЦены = Документы.УстановкаЦен.СоздатьДокумент();
	ДокументЦены.Дата = ТекущаяДата();
	ДокументЦены.Контрагент = Контрагент;
	ДокументЦены.Комментарий = "Загружен из файла";

	// Обходим коллекцию с данными файла и заполняем строки табличной части
	Для Каждого ДанныеСтроки Из ДанныеФайла Цикл

		// Второй раз искать номенклатуру не надо, берем из структуры
		Номенклатура = ДанныеСтроки.НоменклатураСсылка;

		Если Не ЗначениеЗаполнено(Номенклатура) Тогда
			Продолжить;
		КонецЕсли;

		НоваяСтрокаТЧ = ДокументЦены.Цены.Добавить();
		НоваяСтрокаТЧ.Номенклатура = Номенклатура;
		НоваяСтрокаТЧ.Цена = ДанныеСтроки.Цена;

	КонецЦикла;	

	ДокументЦены.Записать();

КонецПроцедуры
```

</details>

6. Убедитесь, что обработка работает корректно

## Задача 3*. Согласование цен и проведение документа

*Это дополнительная задача, реализовывать ее не обязательно.*
*Задача предназначена для тех студентов, которым первые покажутся слишкм простыми.*
*В процессе выполнения не будут даны примеры программного кода.*

### Описание задачи

Если обработка запущена администратором, в форме обработки должен присутствовать флаг "Согласовать цены". Если флаг установлен, документ должен быть согласован при создании и проведен. При снятом флаге - только записан в базу.

### Процесс выполнения

Флаг "Согласовать цены":
1. Создайте новый реквизит обработки, выведите его на форму.
2. Настройте доступность реквизита. Он должен быть доступен только Администратору (пользователю с ролью ПолныеПрава), другие пользователи не должны даже видеть этот флаг
3. В процедуру создания документа, передавайте этот флаг параметром.
4. Значение параметра заполняйте в соответствующий реквизит документа.
5. В зависимости от значения флага записывайте документ в соответствующем режиме - Проведение или Запись.

## Задача 4*. Создание не найденной номенклатуры

*Это дополнительная задача, реализовывать ее не обязательно.*
*Задача предназначена для тех студентов, которым первые покажутся слишкм простыми.*
*В процессе выполнения не будут даны примеры программного кода.*

### Описание задачи

Если номенклатура не найдена, следует предложить пользователю создать ее. ТО есть, вопрос должен выглядеть следующим образом: "Не вся номенклатура найдена. Выберите действие с такой номенклатурой" Варианты ответа:

- Не создавать документ
- Не загружать цены
- Создавать номенклатуру

### Процесс выполнения

1. Перед выводом вопроса пользователю, создайте список значений, в который запишите варианты ответов на вопрос
2. В функции вывода вопроса, вместо *РежимДиалогаВопрос.ДаНет* подставьте этот список значений
3. Пропишите логику создания документа в зависимости от выбранного ответа

- Если пользователь выбрал вариант не создавать документ - ничего не делаем
- Любой другой вариант - передайте в процедуру создания документа, и обрабатывая его создавайте ненайденную номенклатуру, или пропускайте ее.

## Пример

[Пример выполнения домашнего задания](examples/HW_6_1_example.md)

## Критерии оценки

Зачёт ставится, если:

1. Программа запускается, не возникает явных ошибок, исключений при выполнении программы (в том числе, если Вы начали делать дополнительную задачу, ее функционал не должен приводить к ошибкам и исключениям)
2. Реализована обработка для загрузки файла csv
3. Файл csv выслан вместе с решением
4. Проверка обязательных реквизитов реализована корректно
5. Документ создается без лишних строк (по ненайденной номенклатуре) в табличной части
6. Вопрос выводится с использованием асинхронного метода
7. Логика создания документа после ответа на вопрос корректна


