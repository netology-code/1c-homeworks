# Задание к занятию «Программная работа со Ссылочными типами. Обновление конфигурации»

## Цель задания

1. На практике разобрать работу с ссылочными типами через программный код.
2. Научиться правильно прописывать алгоритм обновления конфигурации.
3. Подготовить конфигурацию к последующей работе.

## Чеклист готовности к домашнему заданию

- [ ] Установить учебную платформу версии 8.3.22 или больше.
- [ ] Подготовить разработанную ранее конфигурацию "УправлениеИТФирмой"
- [ ] Просмотреть материал занятия «Программная работа со Ссылочными типами. Обновление конфигурации».

## Инструкция к заданию

1. Решите описанные задачи в конфигураторе.

В тексте задания приведен программный код для решения задач. Конечно, Вы можете использовать его. Но для более продуктивного обучения, старайтесь, сначала, самостоятельно написать код. Если все же, самостоятельно решить задачу не удалось, используйте код из текста задачи но обязательно пройдите его в отладке, посмотрите, что попадает в переменные, как преобразуются значения. Старайтесь максимально детально разобраться в механизме.

2. Протестируйте решение в пользовательском режиме, обязательно введите данные в базу, убедитесь, что все работает.
3. Отправьте на проверку в личном кабинете Нетологии один общий файл базы данных (.dt), содержащей решение всех задач.

## Задача 1. Версия конфигурации

### Описание задачи

Необходимо подготовить конфигурацию к возможным обновлениям написать "программную обвязку" для этого. То есть, реализовать алгоритмы, которые мы потом будем использовать при решении задач.

Добавить константу **ВерсияКонфигурации**, которая будет хранить версию конфигурации из свойств метаданных, с которой программа запускалась в последний раз. Хранение версии в данных позволит обнаружить запуск программы с изменённой версией и запустить код, заполняющий недостающие данные при обновлении версии.

### Процесс выполнения

1. Создайте константу **ВерсияКонфигурации** типа **Строка**, которая будет хранить текущую версию конфигурации. 
2. Добавьте константу в подсистему Справочники (или создайте для нее отдельную подсистему "Служебное", в этой подсистеме снимите флаг "Включать в командный интерфейс")
3. Скройте константу из командного интерфейса, сняв в свойствах флажок «Использовать стандартные команды».
4. Создайте общий модуль **ОбновлениеИнформационнойБазыСервер** с экспортной процедурой **ПриНачалеРаботыСистемы**, которая:

- проверит, совпадает ли версия конфигурации (_Метаданные.Версия_) со значением константы (_Константы.ВерсияКонфигурации.Получить()_);
- при совпадении ничего не сделает;
- при выявлении разницы вызовет ПриИзмененииВерсии(СтараяВерсия, НоваяВерсия);
- ПриИзмененииВерсии, в свою очередь, пока будет только устанавливать значение константы равным новой версии из метаданных

<details>
  <summary>Код</summary>
  
```bsl
Процедура ПриНачалеРаботыСистемы() Экспорт

	НоваяВерсия = Метаданные.Версия;
	СтараяВерсия = Константы.ВерсияКонфигурации.Получить();

	Если НоваяВерсия = СтараяВерсия Тогда
		Возврат;
	КонецЕсли;

	ПриИзмененииВерсии(СтараяВерсия, НоваяВерсия);

КонецПроцедуры

Процедура ПриИзмененииВерсии(СтараяВерсия, НоваяВерсия)

	Константы.ВерсияКонфигурации.Установить(НоваяВерсия);

КонецПроцедуры
```  
</details>

5. Создайте общий модуль **ОбновлениеИнформационнойБазыВызовСервера** (не забудьте установить флаги, чтобы модуль был дступен с клиента) с экспортной процедурой **ПриНачалеРаботыСистемы**, которая вызовет одноименную процедуру в **ОбновлениеИнформационнойБазыСервер**

<details>
  <summary>Код</summary>
  
```bsl
Процедура ПриНачалеРаботыСистемы() Экспорт

	ОбновлениеИнформационнойБазыСервер.ПриНачалеРаботыСистемы();

КонецПроцедуры
```

</details>

6. В модуле приложения пропишите вызов процедуру из **ОбновлениеИнформационнойБазыВызовСервера**

<details>
  <summary>Код</summary>
  
```bsl
Процедура ПриНачалеРаботыСистемы()

	ОбновлениеИнформационнойБазыВызовСервера.ПриНачалеРаботыСистемы();

КонецПроцедуры
```
	
</details>

7. Таким образом, при обновлении информационной базы старой версии на конфигурацию новой версии будут выполнены обработчики обновления, необходимые новой версии. Сами обработчики обновления писать пока не нужно.
8. Проверьте, что при изменении версии в метаданных и запуске программы значение константы обновляется. Открыть константу можно через режим технического специалиста.

<details>
  <summary>Дополнительно. Для лучшего понимания механизма обновления</summary>
  Для лучшего понимания механизма обновления, после завершения задачи выгрузите dt своей базы в текущем состоянии.
</details>

## Задача 2. Виды контактной информации

### Описание задачи

Добавить в справочник «Контрагенты» табличную часть «Контактная информация» для хранения адресов, телефонов и т. п. в разрезе видов контактной информации — так, как это делается в реальных прикладных решениях, вместо хранения каждого вида контактной информации в отдельном реквизите, как мы это делали раньше. 
Для этого потребуется подготовительная работа. Создадим справочник "Виды контактной информации", в котором будет храниться наименование вида информации и ее тип - Адрес, Телефон, EMail, Вебсайт. В справочнике должны быть созданы предопределенные элементы, чтобы заполнить их тип, пропишем обработчик обновления.

Важно! Чтобы проверить успешность выполнения обновления, в пользовательском режиме установите значение константы **ВерсияКонфигурации** на "1.0.0.0" или очистите константу совсем.

### Процесс выполнения

1. Создать перечисление ТипыКонтактнойИнформации со значениями: Адрес, Телефон, EMail, Вебсайт. Укажите подсистему.
2. Создать справочник ВидыКонтактнойИнформации с реквизитом Тип (ПеречислениеСсылка.ТипыКонтактнойИнформации). Укажите подсистему. Справочник будет расширяемым для пользователей, а значение реквизита Тип позволит программе понять, как работать с этим видом Контактной информации.
3. Создать предопределённые виды контактной информации: ЮридическийАдресКонтрагента, ПочтовыйАдресКонтрагента, ФактическийАдресКонтрагента, ТелефонКонтрагента, EMailКонтрагента.
4. В модуле менеджера справочника создать экспортную процедуру **ЗаполнитьПредопределенныеЭлементы**, которая заполнит реквизит Тип у всех предопределённых элементов этого справочника. В Конфигураторе можно задать только код и наименование предопределённых элементов, но не другие реквизиты.

<details>
  <summary>Код</summary>
  
```bsl
Процедура ЗаполнитьПредопределенныеЭлементы() Экспорт

ТипАдрес = Перечисления.ТипыКонтактнойИнформации.Адрес; 

ПредопределенныйОбъект = Справочники.ВидыКонтактнойИнформации.ЮридическийАдресКонтрагента.ПолучитьОбъект();
ПредопределенныйОбъект.Тип = ТипАдрес;
ПредопределенныйОбъект.Записать();

// На самом деле, так как мы находимся в модуле менеджера справочника ВидыКонтактнойИнформации,
// можно не писать "Справочники.ВидыКонтактнойИнформации", мы и так в этом контексте.
ПредопределенныйОбъект = ПочтовыйАдресКонтрагента.ПолучитьОбъект();
ПредопределенныйОбъект.Тип = ТипАдрес;
ПредопределенныйОбъект.Записать();

ПредопределенныйОбъект = ФактическийАдресКонтрагента.ПолучитьОбъект();
ПредопределенныйОбъект.Тип = ТипАдрес;
ПредопределенныйОбъект.Записать();

ПредопределенныйОбъект = ТелефонКонтрагента.ПолучитьОбъект();
ПредопределенныйОбъект.Тип = Перечисления.ТипыКонтактнойИнформации.Телефон;
ПредопределенныйОбъект.Записать();

ПредопределенныйОбъект = EMailКонтрагента.ПолучитьОбъект();
ПредопределенныйОбъект.Тип = Перечисления.ТипыКонтактнойИнформации.EMail;
ПредопределенныйОбъект.Записать();

КонецПроцедуры
```
</details>

5. Установить версию конфигурации в метаданных на следующее значение (например, 1.0.0.1, если До этого Вы не забыли очистить или установить 1.0.0.0).
6. В процедуре **ПриИзмененииВерсии**:
* проверить, не находится ли установленная версия 1.0.0.1 между значениями параметров СтараяВерсия и НоваяВерсия. Если старая версия пуста, значит, она меньше любой текущей;

- если версия 1.0.0.1 находится между старой и новой версией, включая границы, — вызвать процедуру ЗаполнитьПредопределенныеЭлементы().

<details>
  <summary>Код</summary>

```bsl
Процедура ПриИзмененииВерсии(СтараяВерсия, НоваяВерсия)

Если СтараяВерсия < "1.0.0.1" И НоваяВерсия >= "1.0.0.1" Тогда
	Справочники.ВидыКонтактнойИнформации.ЗаполнитьПредопределенныеЭлементы();
КонецЕсли;

Константы.ВерсияКонфигурации.Установить(НоваяВерсия);

КонецПроцедуры
```

</details>
7. Таким образом, и при первом запуске, и при обновлении будут заполнены предопределённые элементы.
8. Проверьте работу в пользовательском режиме

## Задача 3. Перенос контактной информации справочника Контрагенты в табличную часть.

### Описание задачи

Сделать обработчик переноса контактной информации из отдельных реквизитов в табличную часть — так, как это делается при обновлении в реальных прикладных решениях.

Важно! Чтобы проверить успешность переноса, не забудьте предварительно заполнить контактную информацию некоторых контрагентов в отдельных реквизитах.
Для проверки своей работы, проконтролируйте, что в метаданных и в константе ВерсияКонфигурации указано значение 1.0.0.0 или 1.0.0.1 (либо, пустое значение)

### Процесс выполнения

1. Стандартному реквизиту Наименование установите синоним «Краткое наименование».
2. Добавьте табличную часть КонтактнаяИнформация с реквизитами Вид (СправочникСсылка.ВидыКонтактнойИнформации) и Значение (Строка).
3. Прежним реквизитам, отвечавшим за контактную информацию, нужно добавить к наименованию префикс «Удалить» (Например: УдалитьЮридическийАдресКонтрагента) и скрыть их из форм. Совсем удалять их нельзя, чтобы можно было перенести старые данные.
3. Добавьте в форму контрагента, новую табличную часть. (просто перенесите ее из реквизита Объект на форму, на вопрос системы, создавать ли колонки, нажмите Да)

<details>
  <summary>Дополнительно</summary>
  Создайте на форме две страницы - "Основные данные" и "Контактная информация". Разместите реквизиты в одну страницу, а табличную часть в другую. 	
</details>

4. В модуле менеджера справочника создайте экспортную процедуру ЗаполнитьТабличнуюЧастьКонтактнаяИнформация(), которая:

- откроет выборку всех элементов справочника (Справочники.Контрагенты.Выбрать());
- для каждого элемента очистит ТЧ КонтактнаяИнформация и заполнит её значениями старых реквизитов (с префиксом Удалить);
- запишет каждый элемент.

<details>
  <summary>Код</summary>

```bsl
  
Процедура ЗаполнитьТабличнуюЧастьКонтактнаяИнформация() Экспорт

	ЮридическийАдресКонтрагента = Справочники.ВидыКонтактнойИнформации.ЮридическийАдресКонтрагента;
	ФактическийАдресКонтрагента = Справочники.ВидыКонтактнойИнформации.ФактическийАдресКонтрагента;
	ТелефонКонтрагента = Справочники.ВидыКонтактнойИнформации.ТелефонКонтрагента;
	EMailКонтрагента = Справочники.ВидыКонтактнойИнформации.EMailКонтрагента;

	Выборка = Справочники.Контрагенты.Выбрать();
	Пока Выборка.Следующий() Цикл

		КонтрагентОбъект = Выборка.ПолучитьОбъект();
		КонтрагентОбъект.КонтактнаяИнформация.Очистить();

		Если ЗначениеЗаполнено(КонтрагентОбъект.УдалитьЮридическийАдрес) Тогда
			НоваяСтрокаКИ = КонтрагентОбъект.КонтактнаяИнформация.Добавить();
			НоваяСтрокаКИ.Вид = ЮридическийАдресКонтрагента;
			НоваяСтрокаКИ.Значение = КонтрагентОбъект.УдалитьЮридическийАдрес;
		КонецЕсли;

		Если ЗначениеЗаполнено(КонтрагентОбъект.УдалитьФактическийАдрес) Тогда
			НоваяСтрокаКИ = КонтрагентОбъект.КонтактнаяИнформация.Добавить();
			НоваяСтрокаКИ.Вид = ФактическийАдресКонтрагента;
			НоваяСтрокаКИ.Значение = КонтрагентОбъект.УдалитьФактическийАдрес;
		КонецЕсли;

		Если ЗначениеЗаполнено(КонтрагентОбъект.УдалитьТелефон) Тогда
			НоваяСтрокаКИ = КонтрагентОбъект.КонтактнаяИнформация.Добавить();
			НоваяСтрокаКИ.Вид = ТелефонКонтрагента;
			НоваяСтрокаКИ.Значение = КонтрагентОбъект.УдалитьТелефон;
		КонецЕсли;

		Если ЗначениеЗаполнено(КонтрагентОбъект.УдалитьEmail) Тогда
			НоваяСтрокаКИ = КонтрагентОбъект.КонтактнаяИнформация.Добавить();
			НоваяСтрокаКИ.Вид = EMailКонтрагента;
			НоваяСтрокаКИ.Значение = КонтрагентОбъект.УдалитьEmail;
		КонецЕсли;

		КонтрагентОбъект.Записать();

	КонецЦикла;

КонецПроцедуры
```
</details>
	
5. В процедуре **ПриИзмененииВерсии** при обновлении на версию 1.0.0.2 вызвайте процедуру ЗаполнитьТабличнуюЧастьКонтактнаяИнформация().

<details>
  <summary>Код</summary>

```bsl
Процедура ПриИзмененииВерсии(СтараяВерсия, НоваяВерсия)

	Если СтараяВерсия < "1.0.0.1" И НоваяВерсия >= "1.0.0.1" Тогда
		Справочники.ВидыКонтактнойИнформации.ЗаполнитьПредопределенныеЭлементы();
	КонецЕсли;

	Если СтараяВерсия < "1.0.0.2" И НоваяВерсия >= "1.0.0.2" Тогда
		Справочники.Контрагенты.ЗаполнитьТабличнуюЧастьКонтактнаяИнформация();
	КонецЕсли;

	Константы.ВерсияКонфигурации.Установить(НоваяВерсия);

КонецПроцедуры

```

</details>

6. Установите версию в метаданных на 1.0.0.2.
7. Запустите программу и удостоверьтесь, что ранее введенная контактная информация не утеряна и доступна теперь уже через новую табличную часть.

<details>
  <summary>Дополнительно. Для лучшего понимания механизма обновления</summary>
  Если Вы выгрузили после первой задачи dt своей базы, сейчас можно проверить следующее:

1. Сохраниете текущее состояние конфигурации в cf-файл
2. В новую базу данных загрузите dt, который сделали после первого задания
3. Запустите базу, убедитесь что в метаданных и в константе версия 1.0.0.0
4. Загрузите конфигурацию в эту базу. Сейчас в константе хранится 1.0.0.0, а в метаданных версия 1.0.0.2, то есть мы как-бы обновились пропустив релиз 1.0.0.1
5. Запустите пользовательский режим, убедитесь, что процедура обновления на 1.0.0.1 так же была выполнена (у предопределенных видов Контактной Информации заполнены реквизиты).

Таким образом, мы видим, что обработчик прописан верно и будет работать даже если мы обновимся с пропуском какой-то версии.

</details>	

## Задача 4*. Пересчет данных документов за период

*Это дополнительная задача, реализовывать ее не обязательно.*
*Задача предназначена для тех студентов, которым первые покажутся слишкм простыми.*
*В процессе выполнения не будут даны примеры программного кода.*

### Описание задачи

Необходимо реализовать обработку, в которой пользователь мог бы указать Дату начала и Дату окончания, нажать на кнопку и все документы поступления товаров за указанный период пересчитались бы (данные в строках табличной части и общая сумма по документу)

### Процесс выполнения

1. Создайте обработку "Пересчет поступлений", разместите ее в подсистеме Сделки
2. В форме обработки создайте 2 реквизита с типом Дата - ДатаНачалаПериода и ДатаЗавершенияПериода
3. В форме создайте одну команду "Пересчитать документы"
4. При нажатии на кнопку, сформируйте выборку документов ПоступленияТоваровИУслуг, за указанный период. Получайте каждый документ. Перебирайте строки его табличной части и пересчитывайте Сумму в строке. Запишите документ
5. В пользовательском режиме введите тестовые данные (исправьте в нескольких документах Суммы в строках табличной части).
6. Запустите обработку, убедитесь, что документы пересчитались.

## Пример

[Пример выполнения домашнего задания](examples/HW_4_3_example.md)

## Критерии оценки

Зачёт ставится, если:

1. Программа запускается, не возникает явных ошибок, исключений при выполнении программы (в том числе, если Вы начали делать дополнительную задачу, ее функционал не должен приводить к ошибкам и исключениям)
2. Присутствует справочник ВидыКонтактнойИнформации с пятью предопределенными элементами: ЮридическийАдресКонтрагента, ПочтовыйАдресКонтрагента, ФактическийАдресКонтрагента, ТелефонКонтрагента, EMailКонтрагента и реквизитом Тип (ПеречислениеСсылка.ТипыКонтактнойИнформации);
3. В справочнике Контрагенты присутствует табличная часть КонтактнаяИнформация, данные которой выведены на форму контрагента таблицей, а прежние реквизиты, напротив, имеют префикс Удалить и скрыты с формы;
4. Присутствует код, при начале работы системы сравнивающий версию из метаданных и версию из константы, и:

- при переходе на версию 1.0.0.1 или более новую, а также при первом запуске инициирующий заполнение типов предопределенных элементов справочника ВидыКонтактнойИнформации;
- при переходе на версию 1.0.0.2 или более новую, а также при первом запуске инициирующий перенос контактной информации из реквизитов Удалить<...> в ТЧ КонтактнаяИнформация.

5. Введены тестовые данные.

Задачи 1, 2 и 3 обязательны к выполнению (кроме текста под спойлером "Дополнительно" - эти задачи делать не обязательно. Возможно, Вы вернетесь к ним позднее, после того, как изучите дополнительный материал). Задача 4 - не обязательна.

Пожалуйста, присылайте на проверку все задачи сразу, одним файлом выгрузки информационной базы (dt)

Любые вопросы по решению задач задавайте в разделе "Вопросы по заданию" в личном кабинете.
