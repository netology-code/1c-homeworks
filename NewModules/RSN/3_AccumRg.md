# Задание к занятию «Регистры накопления, проведение документов»

*Примерное время выполнения: 20–60 минут*

## Цель задания

1. Изучить работу с регистрами накопления
2. Научиться формировать зааписи в регистрах при помощи документов
3. Подготовить конфигурацию к последующей работе.

## Чеклист готовности к домашнему заданию

- [ ] Установить учебную платформу версии 8.3.22 или больше.
- [ ] Подготовить разработанную ранее конфигурацию "УправлениеИТФирмой"
- [ ] Просмотреть материал занятия «Регистры накопления, проведение документов».

## Инструкция к заданию

1. Решите описанные задачи в конфигураторе.

В тексте задания приведен программный код для решения задач. Конечно, Вы можете использовать его. Но для более продуктивного обучения, старайтесь, сначала, самостоятельно написать код. Если все же, самостоятельно решить задачу не удалось, используйте код из текста задачи но обязательно пройдите его в отладке, посмотрите, что попадает в переменные, как преобразуются значения. Старайтесь максимально детально разобраться в механизме.

2. Протестируйте решение в пользовательском режиме, обязательно введите данные в базу, убедитесь, что все работает.
3. Отправьте на проверку в личном кабинете Нетологии один общий файл базы данных (.dt), содержащей решение всех задач.

## Задача 1. Учет количества товара в регистре накопления

### Описание задачи

Нам необходимо учитывать остатки товаров на складе. Необходимо добавить соответствующий регистр накопления и прописать его заполнение.

### Процесс выполнения

1. Создайте регистр накопления Товары. Вид регистра - Остатки, Измерение - Номенклатура с соответствующим типом данных, Ресурс - Количество с типом данных аналогичным колонке Количество в табличных частях документов. Подсистема - Сделки.
2. На закладке Регистраторы укажите, что движения в этот регистр будут выполнять ПоступлениеТоваровИУслуг и РеализацияТоваровИУслуг
3. Настройте права на регистр - все роли, имеющие права на Проведение документов, должны иметь права на работу с регистром
4. Пропишите обработку проведения для документов. Обратите внимание - перед проведением документа, его табличную часть, лучше свернуть. Так же, обратите внимание, что запись в регистр должна выполняться только для номенклатуры с типом Товар. Вести учет количества услуг не имеет смысл.

<details>
  <summary>Код</summary>
  
Обработка проведения документа Поступления товаров:
```bsl
Процедура ОбработкаПроведения(Отказ, Режим)

	Движения.Товары.Записывать = Истина;

    // Выгружаем табличную часть в таблицу значений
	ТаблицаДляПроведения = Товары.Выгрузить();
	ТаблицаДляПроведения.Свернуть("Номенклатура", "Количество, Сумма");

	ТипУслуга = Перечисления.ТипНоменклатуры.Услуга;

	Для Каждого ТекСтрокаТовары Из ТаблицаДляПроведения Цикл

		Если ТекСтрокаТовары.Номенклатура.ТипНоменклатуры = ТипУслуга Тогда
			// услуги в регистр не пишем
			Продолжить;
		КонецЕсли;

		Движение = Движения.Товары.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Период = Дата;
		Движение.Номенклатура = ТекСтрокаТовары.Номенклатура;
		Движение.Количество = ТекСтрокаТовары.Количество;
	КонецЦикла;

КонецПроцедуры
```

Для реализации товаров, процедура аналогична, но вид движения - расход
</details>

5. На форме обоих документов в режиме конфигуратора выведите переход к движениям по регистру товары

## Задача 2. Учет продаж товаров в разрезе сотрудников

### Описание задачи

Добавим возможность хранить информацию о том, какой менеджер и на какую сумму продал какую номенклатуру. Это может понадобиться, например, для расчета премий сотрудникам, или для анализа, какой товар приносит большую прибыль.

### Процесс выполнения

1. Добавьте регистр накопления "Продажи". Вид регистра - Обороты, Измерения - Сотрудник, Номенклатура с соответствующими типами данных, Ресурс - Сумма с типом данных аналогичным колонке Сумма в табличных частях документов. Подсистема - Сделки.
2. На закладке Регистраторы укажите, что движения в этот регистр будет выполнять только РеализацияТоваровИУслуг
3. Настройте права на регистр - все роли, имеющие права на Проведение документов, должны иметь права на работу с регистром
4. Пропишите обработку проведения для документов. В данном случае, мы будем учитывать и товары и услуги, по этому ключевое слово Продолжить нам не подойдет. Если номенклатура товар - надо записать расход по регистру и во всех случаях делать запись по продажам

<details>
  <summary>Код</summary>

```bsl
Процедура ОбработкаПроведения(Отказ, Режим)

	Движения.Товары.Записывать = Истина;
	Движения.Продажи.Записывать = Истина;

	ТаблицаДляПроведения = Товары.Выгрузить(); // Выгружаем табличную часть в таблицу значений
	ТаблицаДляПроведения.Свернуть("Номенклатура", "Количество, Сумма");

	ТипТовар = Перечисления.ТипНоменклатуры.Товар;

	Для Каждого ТекСтрокаТовары Из ТаблицаДляПроведения Цикл
		
		Если ТекСтрокаТовары.Номенклатура.ТипНоменклатуры = ТипТовар Тогда
			Движение = Движения.Товары.Добавить();
			Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
			Движение.Период = Дата;
			Движение.Номенклатура = ТекСтрокаТовары.Номенклатура;
			Движение.Количество = ТекСтрокаТовары.Количество;
		КонецЕсли;

		Движение = Движения.Продажи.Добавить();
		Движение.Период = Дата;
		Движение.Сотрудник = Ответственный;
		Движение.Номенклатура = ТекСтрокаТовары.Номенклатура;
		Движение.Сумма = ТекСтрокаТовары.Сумма;
		
	КонецЦикла;

КонецПроцедуры
```

</details>

5. На форме документа в режиме конфигуратора выведите переход к движениям по регистру

## Задача 3. Установка цен поставщиков документом

### Описание задачи

Добавим в документ «Установка цен» движение по регистру сведений «ЦеныПоставщиков».

### Процесс выполнения

1. Очистите все записи из регистра сведений «ЦеныПоставщиков», затем донастройте его:

- периодичность - В пределах секунды;
- режим записи - Подчинение регистратору;
- регистратор - Установка цен

2. В документе Установка цен пропишите обработку проведения и в форме документа выведите переход к движениям регистра

<details>
  <summary>Код</summary>

```bsl
Процедура ОбработкаПроведения(Отказ, Режим)

	Движения.ЦеныПоставщиков.Записывать = Истина;
	Для Каждого ТекСтрокаЦены Из Цены Цикл
		Движение = Движения.ЦеныПоставщиков.Добавить();
		Движение.Период = Дата;
		Движение.Номенклатура = ТекСтрокаЦены.Номенклатура;
		Движение.Контрагент = Контрагент;
		Движение.Цена = ТекСтрокаЦены.Цена;
		Движение.Ответственный = Ответственный;
	КонецЦикла;

КонецПроцедуры
```

</details>

<details>
  <summary>Дополнительно</summary>
Перед проведением документа, реализуйте проверку, что в табличной части нет повторяющихся номенклатур. Если есть - выведите предупреждение пользователю и откажитесь от проведения документа
</details>

## Пример

[Пример выполнения домашнего задания](examples/HW_5_3_example.md)

## Критерии оценки

Зачёт ставится, если:

1. Программа запускается, не возникает явных ошибок, исключений при выполнении программы (в том числе, если Вы начали делать дополнительную задачу, ее функционал не должен приводить к ошибкам и исключениям)
2. Документы «Поступление товаров и услуг» и «Реализация товаров и услуг» должны добавлять приход и расход по регистру накопления «Товары»
3. Документ «Реализация товаров и услуг» должен фиксировать продажи по сотрудникам и номенклатуре
4. Документ «Установка цен» фиксирует цены поставщиков
5. Введены тестовые данные

Все задачи обязательны к выполнению (кроме текста под спойлером "Дополнительно" - эти задачи делать не обязательно. Возможно, Вы вернетесь к ним позднее, после того, как изучите дополнительный материал).

Пожалуйста, присылайте на проверку все задачи сразу, одним файлом выгрузки информационной базы (dt)

Любые вопросы по решению задач задавайте в чате учебной группы.
