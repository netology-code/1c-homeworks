# Задание к занятию «Запись и чтение регистров cведений»

Все задачи обязательны к выполнению, кроме текста под спойлером "Дополнительно": эти задачи делать необязательно. Возможно, Вы вернетесь к ним позднее, после того, как изучите дополнительный материал. 

Пожалуйста, присылайте на проверку все задачи сразу, одним файлом выгрузки информационной базы (dt). Любые вопросы по решению задач задавайте в чате учебной группы.

## Цель задания

1. Изучить программную работу с регистрами сведений.
2. Научиться обращаться к данным регистра из программного кода.
3. Подготовить конфигурацию к последующей работе.

## Чеклист готовности к домашнему заданию

- [ ] Установить учебную платформу версии 8.3.22 или больше.
- [ ] Подготовить разработанную ранее конфигурацию "УправлениеИТФирмой"
- [ ] Просмотреть материал занятия «Запись и чтение регистров cведений».

## Инструкция к заданию

1. Решите описанные задачи в конфигураторе.

В тексте задания приведен программный код для решения задач. Конечно, Вы можете использовать его. Но для более продуктивного обучения, старайтесь, сначала, самостоятельно написать код. Если все же, самостоятельно решить задачу не удалось, используйте код из текста задачи но обязательно пройдите его в отладке, посмотрите, что попадает в переменные, как преобразуются значения. Старайтесь максимально детально разобраться в механизме.

2. Протестируйте решение в пользовательском режиме, обязательно введите данные в базу, убедитесь, что все работает.
3. Отправьте на проверку в личном кабинете Нетологии один общий файл базы данных (.dt), содержащей решение всех задач.

## Задача 1. Пакетное создание цен

### Описание задачи

Необходимо создать обработку для пакетной установки цен продажи номенклатуры.

### Процесс выполнения

1. Создайте новую обработку "УстановкаЦенПродажи". Разместите ее в подсистеме "Сделки"
2. Создайте основную форму обработки
3. На форме обработки создайте реквизиты:

- ФиксированнаяЦена (Тип данных - такой же, как у Цены в регистре сведений ЦеныПродажиНоменклатуры)
- СписокНоменклатуры (ТаблицаЗначений с одной колонкой Номенклатура - СправочникСсылка.Номенклатура)

4. Перенесите реквизиты на форму:

- ФиксированнаяЦена: Вид - Поле ввода
- СписокНоменклатуры: Поле таблицы, колонку вывести на форму

5. Создайте команду "Установить" и разместите ее на форме обработки, установите свойство "КнопкаПоУмолчанию" в Истина.
6. Назначьте обработчик события нажатия на кнопку, в котором:

- На сервере переберите всю номенклатуру, указанную в табличной части
- Для каждой номенклатуры создайте запись в регистре с текущей датой и ценой, указанной в поле Фиксированная цена

<details>
  <summary>Код</summary>
  
```bsl
&НаКлиенте
Процедура Установить(Команда)

	УстановитьЦенуНаСервере();

КонецПроцедуры

&НаСервере
Процедура УстановитьЦенуНаСервере()

	Для Каждого СтрокаСписка Из СписокНоменклатуры Цикл

		// В данном случае, мы всегда пишем данные по новому набору измерений, по этому
		// проще использовать менеджер записи, а не набор записей
		МенеджерЗаписи = РегистрыСведений.ЦеныПродажиНоменклатуры.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Номенклатура = СтрокаСписка.Номенклатура;
		МенеджерЗаписи.Период = ТекущаяДата();
		МенеджерЗаписи.Цена = ФиксированнаяЦена;
		МенеджерЗаписи.Записать();
		
	КонецЦикла;

КонецПроцедуры

```

</details>

<details>
  <summary>Дополнительно</summary>

Проконтролируйте, что перед записью данных регистра, у каждой записи установлен Ответственный. Если значение в реквизит не задано, устанавливайте сотрудника, указанного в параметре сеанса.
Очевидно, это надо делать при любой записи регистра, по этому прописать обработчик следует в модуле Набора записей.

</details>

7. Введите тестовые данные, убедитесь, что установка цены работает.

## Задача 2. Пакетное удаление цен

### Описание задачи

Добавить в обработку функцию удаления цен выбранной номенклатуры

### Процесс выполнения

1. В форме обработки добавьте реквизит ВариантУстановки (Число, Длина - 1, Точность - 0, не отрицательное)
2. Перенесите реквизит на форму:

- Вид - Поле переключателя
- ВидПереключателя - Тумблер
- СписокВыбора - 0 - "Фиксированная цена", 1 - "Удаление цен"
- ПоложениеЗаголовка - нет
- КоличествоКолонок - 1

3. При изменении переключателя, должна меняться доступность поля Фиксированная цена. Если указан не 0, поле должно быть недоступно пользователю.

<details>
  <summary>Код</summary>
  
```bsl
&НаКлиенте
Процедура ВариантУстановкиПриИзменении(Элемент)

	Элементы.ФиксированнаяЦена.Доступность = (ВариантУстановки = 0);

КонецПроцедуры
```

</details>

<details>
  <summary>Дополнительно</summary>

Разместите поле так, чтобы фиксированная цена располагалась напротив одноименного значения переключателя (см. пример выполнения ДЗ.)

</details>

4. Допишите установку цен таким образом, чтобы если в ВариантУстановки указан 0 - отрабатывал бы алгоритм из первой задачи, если указано 1 - надо удалить все цены выбранных номенклатурных позиций.

<details>
  <summary>Код</summary>
  
```bsl
&НаСервере
Процедура УстановитьЦенуНаСервере()

	Если ВариантУстановки = 0 Тогда
		УстановитьФиксированнуюЦену(); // в этой процедуре код из прошлой задачи
	ИначеЕсли ВариантУстановки = 1 Тогда
		УдалитьЦеныНоменклатуры();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УдалитьЦеныНоменклатуры()
	
	Для Каждого СтрокаСписка Из СписокНоменклатуры Цикл
		
		// Нам необходимо очистить все записи регистра по указанной номенклатуре
		// Создадим набор записей с отбором по номенклатуре и, не прочитывая,
		// запишем его (так, будет записан пустой набор с отбором по номенклатуре)
		НаборЗаписей = РегистрыСведений.ЦеныПродажиНоменклатуры.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Номенклатура.Установить(СтрокаСписка.Номенклатура);    
		НаборЗаписей.Записать();
		
	КонецЦикла;
	
КонецПроцедуры
```

</details>

5. Убедитесь, что записи по всем выбранным номенклатурным позициям очищаются.

## Задача 3. Пакетное изменение цен

### Описание задачи

Добавить в обработку функцию изменения цен выбранной номенклатуры на указанный пользователем процент

### Процесс выполнения

1. В список выбора переключателя добавьте вариант 2 - Процент от текущей цены
2. В форме обработки добавьте реквизит ПроцентОтТекущейЦены (Число, Длина - 5, Точность - 2)
3. Вынесите поле ввода на форму. Его доступность, так же, как в прошлой задачи должна зависеть от выбранного варианта установки цены. При этом, не забудьте, что устанавливать доступность надо не только при изменении Варианта установки но и при открытии формы.

<details>
  <summary>Дополнительно</summary>

Постарайтесь разместить поле напротив нового значения переключателя (см. пример выполнения ДЗ.) Для этого может понадобиться создать пустое пространство, можно для этого применить Декорацию надпись с пустым заголовком.

</details>

4. Доработайте установку цены таким образом, что если в ВариантУстановки указано значение 2:

- Для каждой номенклатуры создается набор записей
- Набор перебирается в цикле, определяется наиболее поздняя запись
- Цена из самой поздней записи увеличивается на указанный в поле процент
- Создается новая запись в регистре с текущей датой и номенклатурой, и новым рассчитанным значением цены.

<details>
  <summary>Код</summary>
  
```bsl
&НаСервере
Процедура УстановитьЦенуНаСервере()

	Если ВариантУстановки = 0 Тогда
		УстановитьФиксированнуюЦену();
	ИначеЕсли ВариантУстановки = 1 Тогда
		УдалитьЦеныНоменклатуры();
	ИначеЕсли ВариантУстановки = 2 Тогда
		УстановитьЦеныНоменклатурыПроцентом();
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура УстановитьЦеныНоменклатурыПроцентом()

	Для Каждого СтрокаСписка Из СписокНоменклатуры Цикл

		ПоследняяЦена = ПолучитьПоследнююЦенуНоменклатуры(СтрокаСписка.Номенклатура);
		
		// Если цена номенклатуры не установлена, или установлена в 0, не будем создавать новую запись
		Если ПоследняяЦена = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяЦена = ПоследняяЦена * (1 + ПроцентОтТекущейЦены / 100); 

		// В установке фиксированной цены, так же, чтобы избежать дублирования кода, 
		// лучше вызывать этот метод
		УстановитьНовуюЦенуНоменклатуры(СтрокаСписка.Номенклатура, НоваяЦена);
		
	КонецЦикла;

КонецПроцедуры

&НаСервере
Функция ПолучитьПоследнююЦенуНоменклатуры(Номенклатура)

	// Готовим структуру для хранения последней цены
	ПоследняяЦена = Новый Структура;
	ПоследняяЦена.Вставить("Дата", Дата(1, 1, 1));
	ПоследняяЦена.Вставить("Цена", 0);

	// Создадим набор записей с отбором по номенклатуре Прочитаем его
	НаборЗаписей = РегистрыСведений.ЦеныПродажиНоменклатуры.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Номенклатура.Установить(Номенклатура);     
	НаборЗаписей.Прочитать(); 

	// В цикле переберем Набор. При этом, если мы найдем цену установленную позднее 
	// текущей записанной в структуру, перепишем структуру
	// (в целом, механизм похож на поиск самого длинного и самого короткого слова в тексте)
	Для Каждого Запись Из НаборЗаписей Цикл

		// Если дата в структуре больше, чем в записи, эта запись была раньше, пропускаем
		Если ПоследняяЦена.Дата > Запись.Период Тогда
			Продолжить;
		КонецЕсли;

		ПоследняяЦена.Дата = Запись.Период;
		ПоследняяЦена.Цена = Запись.Цена;

	КонецЦикла;
	
	Возврат ПоследняяЦена.Цена;

КонецФункции

&НаСервере
Процедура УстановитьНовуюЦенуНоменклатуры(Номенклатура, Цена)

	МенеджерЗаписи = РегистрыСведений.ЦеныПродажиНоменклатуры.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Номенклатура = Номенклатура;
	МенеджерЗаписи.Период = ТекущаяДата();
	МенеджерЗаписи.Цена = Цена;
	МенеджерЗаписи.Записать();

КонецПроцедуры
```

</details>

5. Убедитесь, что цены всех выбранных номенклатур меняются на указанный процент

## Задача 4. Улучшение обработки

### Описание задачи

Необходимо упростить работу пользователя с обработкой, а именно:

1. Реализовать кнопку "Подбор", для более удобного выбора номенклатуры
2. Добавить возможность указания групп номенклатуры. При указании группы, должны быть обработаны все входящие в нее элементы справочника, в том числе вложенные в другие группы, внутри указанной.

### Процесс выполнения

1. Для колонки таблицы формы установите свойство ВыборГруппИЭлементов в значение "Групп и элементов". Так, мы разрешим пользователю указывать группы
2. Измените процедуру установки цен таким образом:

- В самом начале обработки, создайте массив
- Переберите таблицу в цикле
  - если в строке указан элемент справочника, добавьте его в массив
  - если указана группа, создайте выборку по группе, обрабатывайте ее аналогично
    - если в выборку попал элемент - добавьте его в массив
    - если попала группа - создайте выборку по ней и обрабатывайте
    - и т.д.
- полученный массив, передавайте в процедуры установки цен, в них работайте уже не с таблицей формы, а с номенклатурой из этого массива

<details>
  <summary>Код</summary>
  
```bsl
&НаСервере
Процедура УстановитьЦенуНаСервере()

	ВсяНоменклатураКОбработке = НоменклатураКОбработке();
	
	Если ВариантУстановки = 0 Тогда
		УстановитьФиксированнуюЦену(ВсяНоменклатураКОбработке);
	ИначеЕсли ВариантУстановки = 1 Тогда
		УдалитьЦеныНоменклатуры(ВсяНоменклатураКОбработке);
	ИначеЕсли ВариантУстановки = 2 Тогда
		УстановитьЦеныНоменклатурыПроцентом(ВсяНоменклатураКОбработке);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Функция НоменклатураКОбработке()

	ВсяНоменклатураКОбработке = Новый Массив;
	Для Каждого СтрокаСписка Из СписокНоменклатуры Цикл
		
		НоменклатураВСтроке = СтрокаСписка.Номенклатура;
		Если НоменклатураВСтроке.ЭтоГруппа Тогда
			ДополнитьМассивЭлементамиВложеннымиВГруппу(НоменклатураВСтроке, ВсяНоменклатураКОбработке);
		Иначе
			ВсяНоменклатураКОбработке.Добавить(НоменклатураВСтроке);
		КонецЕсли;
		
	КонецЦикла;

	Возврат ВсяНоменклатураКОбработке;

КонецФункции

Процедура ДополнитьМассивЭлементамиВложеннымиВГруппу(ГруппаНоменклатуры, ВсяНоменклатураКОбработке)

	Выборка = Справочники.Номенклатура.Выбрать(ГруппаНоменклатуры); // Отбор по Родителю
	Пока Выборка.Следующий() Цикл
		Если Выборка.ЭтоГруппа Тогда                                                     
			// Используем рекурсию, чтобы вызвать из процедуры саму себя.
			// Таким образом, нам не важна глубина иерархии, будут пройдены все уровни.
			ДополнитьМассивЭлементамиВложеннымиВГруппу(Выборка.Ссылка, ВсяНоменклатураКОбработке); 
		Иначе
			ВсяНоменклатураКОбработке.Добавить(Выборка.Ссылка);
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

// процедуры установки цен доработайте самостоятельно
```

</details>

3. Добавьте для табличной части кнопку Подбор и собыие обработки выбора. При открытии формы установите для нее параметр "ВыборГруппИЭлементов" значением ИспользованиеГруппИЭлементов.ГруппыИЭлементы

<details>
  <summary>Код</summary>

```bsl
ПараметрыФормы = Новый Структура;
ПараметрыФормы.Вставить("ВыборГруппИЭлементов", ИспользованиеГруппИЭлементов.ГруппыИЭлементы);
// установите другие необходимые параметры

ОткрытьФорму("Справочник.Номенклатура.ФормаВыбора", ПараметрыФормы, Элементы.СписокНоменклатуры);
```

</details>

## Пример

[Пример выполнения домашнего задания](examples/HW_5_2_example.md)

## Критерии оценки

Зачёт ставится, если:

1. Программа запускается, не возникает явных ошибок, исключений при выполнении программы (в том числе, если Вы начали делать дополнительную задачу, ее функционал не должен приводить к ошибкам и исключениям)
2. В конфигурации создана обработка УстановкаЦенПродажи, которая

- содержит таблицу формы Номенклатура для указания элементов одноименного справочника
- переключатель с тремя вариантами установки цен:
  - Фиксированная цена
  - Процент от текущей
  - Удаление цен
- Кнопку "Установить" при нажатии на которую, отрабатывает установка цены в соответствии с выбранным вариантом, для указанной в таблице номенклатуры

3. Корректно обрабатывает установка цен для элементов вложенных в выбранные группы
4. Введены тестовые данные

